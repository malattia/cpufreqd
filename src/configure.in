# Process this file with autoconf to produce a configure script.
AC_INIT([cpufreqd],[2.0.0-beta4],[malattia@gmail.com], [cpufreqd])
AC_CANONICAL_TARGET([])

AM_INIT_AUTOMAKE(1.8 dist-bzip2 foreign)

AC_CONFIG_SRCDIR([cpufreqd.h])
AM_CONFIG_HEADER(config.h)

# libtool
AC_LIBTOOL_DLOPEN
AC_ENABLE_SHARED
AC_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS([fcntl.h limits.h syslog.h unistd.h getopt.h errno.h sys/types.h sys/stat.h signal.h ctype.h dlfcn.h libgen.h cpufreq.h])

# SENSORS support
AC_ARG_WITH([sensors],
	[  --with-sensors\[=PATH\]        sensors support - will provide an additional cpufreqd plugin, needs libsensors [default=autodetected]],
	[sensors_with=$withval],
	[sensors_with=yes]
	)

OLD_CFLAGS="$CFLAGS"
OLD_LDFLAGS="$LDFLAGS"

if test "x$sensors_with" != xno; then

	if test "x$sensors_with" != xyes; then
		CFLAGS="$CFLAGS -I${sensors_with}/include"
		LDFLAGS="$LDFLAGS -L${sensors_with}/lib -lsensors"
	else
		LDFLAGS="$LDFLAGS -lsensors"
	fi

	AC_CHECK_HEADER([sensors/sensors.h],
		[ AC_CHECK_LIB([sensors], [sensors_init],
			[ AC_DEFINE(SENSORS_SUPPORT, , [Define this to compile sensors support])
			if test "x$sensors_with" != xyes; then
				ADD_CFLAGS="$ADD_CFLAGS -I${sensors_with}/include"
				ADD_LDFLAGS="$ADD_LDFLAGS -L${sensors_with}/lib -lsensors"
			else
				ADD_LDFLAGS="$ADD_LDFLAGS -lsensors"
			fi
			sensors_support=success
			],
			[sensors_support=failed])
		],
		[sensors_support=failed])
fi

CFLAGS="$OLD_CFLAGS"
LDFLAGS="$OLD_LDFLAGS"

if test "x$sensors_support" = xfailed && test "x$sensors_with" != xyes; then
	echo '***************************************************'
	echo '***      ERROR WHILE CONFIGURING CPUFREQD       ***'
	echo '***************************************************'
	AC_MSG_ERROR([Failed to find requested libsensors development files in ${sensors_with}])
fi
AM_CONDITIONAL(SENSORS_SUPPORT, test x"${sensors_support}" = xsuccess)

DISABLED_PLUGINS=
ENABLED_PLUGINS=" acpi_battery acpi_ac acpi_temperature apm cpu nforce2 programs"
if test x"${sensors_support}" = xsuccess; then
	ENABLED_PLUGINS="$ENABLED_PLUGINS sensors"
else
	DISABLED_PLUGINS="$DISABLED_PLUGINS sensors"
fi

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

# Checks for libraries.
AC_CHECK_LIB([dl], [dlopen])
AC_CHECK_LIB([cpufreq], [cpufreq_cpu_exists])

# Defines two useful paths in order to remove hardcoded paths
if test "x$prefix" = xNONE; then
  prefix="${ac_default_prefix}"
fi
   
if test "x$exec_prefix" = xNONE; then
  exec_prefix="${prefix}"
fi

CPUFREQD_LIBDIR="$libdir/"
eval CPUFREQD_LIBPATH="$CPUFREQD_LIBDIR"
AC_DEFINE_UNQUOTED(CPUFREQD_LIBDIR,"$CPUFREQD_LIBPATH",[Define this to plugins dir location])
AC_SUBST(CPUFREQD_LIBPATH)

CPUFREQD_CONFDIR="$sysconfdir/"
eval CPUFREQD_CONFPATH="$CPUFREQD_CONFDIR"
AC_DEFINE_UNQUOTED(CPUFREQD_CONFDIR,"$CPUFREQD_CONFPATH",[Define this to configuration dir location])
AC_SUBST(CPUFREQD_CONFPATH)

CPUFREQD_STATEDIR="$localstatedir/"
eval CPUFREQD_STATEPATH="$CPUFREQD_STATEDIR"
AC_DEFINE_UNQUOTED(CPUFREQD_STATEDIR,"$CPUFREQD_STATEPATH",[Define this to local state dir location])
AC_SUBST(CPUFREQD_STATEPATH)

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_STAT
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([memset realpath strerror strstr])

AC_SUBST(ADD_CFLAGS)
AC_SUBST(ADD_LDFLAGS)

AC_CONFIG_FILES([Makefile])
AC_OUTPUT

echo "
cpufreqd configuration:
--------------------------------------------------
Compiler:		${CC}
Enabled plugins:	${ENABLED_PLUGINS}
Disabled plugins:	${DISABLED_PLUGINS}
--------------------------------------------------
"
